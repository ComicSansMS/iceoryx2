# Copyright (c) 2024 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache Software License 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0, or the MIT license
# which is available at https://opensource.org/licenses/MIT.
#
# SPDX-License-Identifier: Apache-2.0 OR MIT

package(default_visibility = ["//visibility:public"])

load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_rust//rust:defs.bzl", "rust_library")

# Generate the binding lib
cc_library(
    name = "iceoryx2-pal-posix-c-lib",
    srcs = [
        "src/c/dirent.c",
        "src/c/sigaction.c",
        "src/c/socket_macros.c",
    ],
    hdrs = glob(["src/c/**"]),
    strip_include_prefix = "src/c",
    linkstatic = True,
    visibility = ["//visibility:public"],
)

# Generate the Rust binding file
genrule(
    name = "iceoryx2-pal-posix-bindgen",
    srcs = [
        "src/c/posix.h",
        "@bindgen//:bindgen-cli",
    ],
    outs = ["src/posix_generated.rs"],
    cmd = "$(execpath @bindgen//:bindgen-cli) --use-core --blocklist-type max_align_t $(location src/c/posix.h) --output $(OUTS)",
)

# Create the actual crate
rust_library(
    name = "iceoryx2-pal-posix",
    srcs = glob(["src/**/*.rs"]) + [ ":src/posix_generated.rs" ],
    deps = [
        "//iceoryx2-pal/concurrency-sync:iceoryx2-pal-concurrency-sync",
        "//iceoryx2-pal/configuration:iceoryx2-pal-configuration",
        "@crate_index//:lazy_static",
        ":iceoryx2-pal-posix-c-lib",
    ],
    rustc_env = {
        # - the file is generated in 'posix/src'
        # - 'lib.rs' expects the location at 'env!("OUT_DIR")/posix_generated.rs'
        # - therefore 'OUT_DIR' needs to point to the same dir where 'lib.rs' is located
        "OUT_DIR": "."
    },
)

# TODO: [349] add feature flags
# TODO: [349] add tests
